//chapter 2 events

EventPointerTable(10, MyEvents) //10 is ch1 events

MyEvents:

POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno Dunno Dunno
POIN Tutorial
POIN TrapData TrapData
POIN Units Units
POIN $0 $0 $0 $0 $0 $0
POIN BeginningScene EndingScene

///

TurnBasedEvents:
TurnEventEnemy(0x0,DanteTalks,1)
TurnEventPlayer(0x0,EnemyReinforcementsLoad,4)
TurnEventPlayer(0x0,EnemyReinforcementsBottomLoad,4)
TurnEventPlayer(0x0,LoadAshur,6)
TurnEventPlayer(0x0,EnemyReinforcementsTopLoad,8)
END_MAIN

ALIGN 32 //CharacterBasedEvents have to be 32-Aligned

CharacterBasedEvents:
CharacterEvent(0x8,RecruitYakov,Safiyah,Yakov)
CharacterEvent(0x8,RecruitYakov,Naiah,Yakov)
CharacterEvent(0x9,RecruitAshur,Yakov,Ashur)
END_MAIN

LocationBasedEvents:
House(0xb,VisitHut,6,11)
END_MAIN

MiscBasedEvents:
AFEV 0x0 CheckLords 0x0
DefeatAll(EndingScene)
END_MAIN

Dunno:
END_MAIN

Tutorial:
END_MAIN

TrapData:
END_MAIN

ALIGN 4

Units:
UNIT

///

BeginningScene:
LOMA 0x02
CAMERA [0,0]
LOAD1 1 EvilCutscene
ENUN
FADU 5
STAL 30
MUSC Lost_Heart
SetBackground(0x2a)
Text(Ch2EvilText)
NoFade
MUSCSLOW Silent
FADI 5
REMOVEALL_RED
LOMA 0x01
LOAD1 1 EnemyUnits
ENUN
LOAD1 1 YakovSpawn
ENUN
CAMERA [0,13]
FADU 5
STAL 30
MUSC 0x5d
SetBackground(0x30)
Text(Ch2YakovOpening)
EndConvo
CAMERA [14,21]
LOAD1 2 PlayerUnits1
ENUN
STAL 30
SetBackground(0x1c)
Text(Ch2StartText)
EndConvo
LOAD1 0 PlayerUnits2
ENUN
ENDA

EndingScene:
MUSC 0x92
CHECK_EXISTS Ashur
BNE 0x2 0xC 0x0
	SetBackground(0x26)
	Text(Ch2EndingScene1)
	GOTO 0x3
LABEL 0x2
SetBackground(0x26)
Text(Ch2EndingScene2)
LABEL 0x3
FADI 3
MoveToChapter(0x0)
ENDA

///

CheckNoUnits:
ASMC GetBlueUnitCountASMC
SVAL 0x7 0x4
BLT 0x0 0xC 0x7 // If there are less than 2 alive, game over.
	GOTO 0x01
LABEL 0x00
ASMC 0x00D391 //GameOver
LABEL 0x01
NoFade
ENDA

CheckLords:
CHECK_ALIVE Naiah
BNE 0x3 0xC 0x0
	ASMC 0x00D391 //GameOver
LABEL 0x3
CHECK_ALIVE Idi
BNE 0x4 0xC 0x0
	ASMC 0x00D391 //GameOver
LABEL 0x4
NoFade
ENDA
///

VisitHut:
VillageEventItem(Ch2HutText,0x0,Elixir)
ENDA

///

DanteTalks:
CAMERA [0,0]
STAL 30
MUSI
SetBackground(0x26)
Text(Ch2TalkDante)
EndConvo
MURE 2
NoFade
ENDA

RecruitYakov:
MUSI
MUSS 0x30 //comrades
Text(Ch2TalkYakov)
MURE 2
CUSA Yakov
NoFade
ENDA

RecruitAshur:
MUSI
MUSS 0x30
Text(Ch2YakovTalkAshur)
MURE 2
CUSA Ashur
NoFade
ENDA

LoadAshur:
MUSI
CAMERA [0,0]
LOAD1 1 AshurSpawn
ENUN
Text(Ch2AshurText)
MUNO
NoFade
ENDA

EnemyReinforcementsLoad:
CAMERA [0,6]
LOAD1 1 EnemyReinforcements1
ENUN
STAL 10
LOAD1 1 EnemyReinforcements2
ENUN
STAL 10
NoFade
ENDA

EnemyReinforcementsBottomLoad:
CAMERA [0,0]
LOAD1 1 EnemyReinforcements3
ENUN
STAL 10
NoFade
ENDA

EnemyReinforcementsTopLoad:
CAMERA [0,0]
LOAD1 1 EnemyReinforcements4
ENUN
STAL 10
NoFade
ENDA

//

EvilCutscene:
UNIT CutsceneUnit Necromancer CutsceneUnit Level(1,Enemy,False) [7,3] 0x0 0x0 0x0 0x1 [FireFang,Vulnerary] NoAI
UNIT CutsceneUnit AmaniMaleClass 0 Level(1,Enemy,False) [7,2] 0x0 0x0 0x0 0x1 [FireFang,Vulnerary] NoAI
UNIT

PlayerUnits1:
UNIT Naiah Mauthedoog Eirika Level(1,Ally,False) [9,21] 0x0 0x0 0x1 REDA9R20 [FireFang,Vulnerary] NoAI
UNIT Idi Mauthedoog Eirika Level(1,Ally,False) [10,21] 0x0 0x0 0x1 REDA9R19 [FireFang,Vulnerary] NoAI
UNIT Safiyah Archer_F Naiah Level(1,Ally,False) [10,21] 0x0 0x0 0x1 REDA10R20 [IronBow,Vulnerary] NoAI
UNIT

PlayerUnits2:
UNIT Ayanna Monk_F Naiah Level(2,Ally,False) [8,21] 0x0 0x0 0x1 REDA8R20 [Lightning,Vulnerary] NoAI
UNIT Elena Cleric Naiah Level(5,Ally,False) [9,21] 0x0 0x0 0x0 0x1 [Mend,Vulnerary,GoddessIcon] NoAI
UNIT

YakovSpawn:
UNIT Yakov Brigand Yakov Level(7,NPC,False) [6,11] 0x0 0x0 0x1 REDA6R12 [SteelAxe,Vulnerary] AttackInRangeAI
UNIT

AshurSpawn:
UNIT Ashur Thief 0x0 Level(5,Enemy,False) [2,0] 0x0 0x0 0x1 REDA2R1 [IronSword,Lockpick,Vulnerary] NeverMoveAI
UNIT

EnemyUnits:
UNIT Dante Mercenary Dante Level(7,Enemy,False) [9,1] 0x0 0x0 0x0 0x1 [SteelSword] NeverMoveAI
UNIT AxeBandit Brigand Dante Level(5,Enemy,True) [11,1] DropItem 0x0 0x0 0x1 [Swordreaver] DefaultAI
UNIT AxeBandit Brigand Dante Level(5,Enemy,True) [12,2] 0x0 0x0 0x0 0x1 [IronAxe] DefaultAI
//UNIT DarkMage Shaman Dante Level(7,Enemy,True) [7,1] 0x0 0x0 0x0 0x1 [Flux] NeverMoveAI
//UNIT DarkMage Shaman Dante Level(7,Enemy,True) [6,2] 0x0 0x0 0x0 0x1 [Flux] NeverMoveAI
UNIT AxeBandit Fighter Dante Level(4,Enemy,True) [8,3] 0x0 0x0 0x0 0x1 [IronAxe] AttackInRangeAI
UNIT SwordBandit Mercenary Dante Level(5,Enemy,True) [9,3] 0x0 0x0 0x0 0x1 [IronSword] AttackInRangeAI
UNIT AxeBandit Fighter Dante Level(4,Enemy,True) [10,3] 0x0 0x0 0x0 0x1 [IronAxe] AttackInRangeAI
UNIT Amani18 AmaniMaleClass Dante Level(4,Enemy,True) [1,15] 0x0 0x0 0x0 0x1 [FireFang] AttackInRangeAI
UNIT Amani6 AmaniMaleClass Dante Level(4,Enemy,True) [6,1] 0x0 0x0 0x0 0x1 [FireFang] AttackInRangeAI
UNIT AxeBandit Brigand Dante Level(4,Enemy,True) [2,8] 0x0 0x0 0x0 0x1 [IronAxe] AttackInRangeAI
UNIT AxeBandit Brigand Dante Level(4,Enemy,True) [10,13] 0x0 0x0 0x0 0x1 [IronAxe] DefaultAI
UNIT AxeBandit Brigand Dante Level(4,Enemy,True) [6,7] 0x0 0x0 0x0 0x1 [IronAxe] DefaultAI
UNIT AxeBandit Brigand Dante Level(4,Enemy,True) [3,20] 0x0 0x0 0x0 0x1 [IronAxe] DefaultAI
UNIT AnimaMage Mage Dante Level(5,Enemy,True) [9,8] 0x0 0x0 0x0 0x1 [Thunder] AttackInRangeAI
UNIT AnimaMage Mage Dante Level(5,Enemy,True) [3,10] 0x0 0x0 0x0 0x1 [Fire] DefaultAI
//UNIT AnimaMage Mage Dante Level(5,Enemy,True) [0,18] 0x0 0x0 0x0 0x1 [Fire] DefaultAI
UNIT

EnemyReinforcements1:
UNIT AxeBandit Fighter Dante Level(4,Enemy,True) [0,8] 0x0 0x0 0x0 0x1 [IronAxe] DefaultAI
UNIT SwordBandit Mercenary Dante Level(4,Enemy,True) [0,9] 0x0 0x0 0x0 0x1 [IronSword] DefaultAI
UNIT

EnemyReinforcements2:
UNIT AnimaMage Mage Dante Level(4,Enemy,True) [14,5] 0x0 0x0 0x0 0x1 [Fire] DefaultAI
UNIT AnimaMage Mage Dante Level(5,Enemy,True) [14,9] 0x0 0x0 0x0 0x1 [Fire] DefaultAI
UNIT

EnemyReinforcements3:
UNIT LanceBandit Deserter Dante Level(4,Enemy,True) [1,0] 0x0 0x0 0x1 REDA1R2 [IronLance] DefaultAI
//UNIT LanceBandit Deserter Dante Level(4,Enemy,True) [2,0] 0x0 0x0 0x1 REDA2R1 [SteelLance] DefaultAI
UNIT LanceBandit Deserter Dante Level(4,Enemy,True) [3,0] 0x0 0x0 0x1 REDA3R2 [IronLance] DefaultAI
UNIT

EnemyReinforcements4:
UNIT DarkMage Shaman Dante Level(5,Enemy,True) [7,0] 0x0 0x0 0x0 0x1 [Flux] AttackInRangeAI
UNIT DarkMage Shaman Dante Level(5,Enemy,True) [11,0] 0x0 0x0 0x0 0x1 [Flux] AttackInRangeAI
UNIT

MESSAGE Chpater2 ends at offset CURRENTOFFSET