//chapter 1 events

EventPointerTable(7, MyEvents) //7 is prologue events

MyEvents:

POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno Dunno Dunno
POIN Tutorial
POIN TrapData TrapData
POIN Units Units
POIN $0 $0 $0 $0 $0 $0
POIN BeginningScene EndingScene

///

TurnBasedEvents:
TurnEventPlayer(0x0,EnemiesCome1,3)
END_MAIN

ALIGN 32 //CharacterBasedEvents have to be 32-Aligned

CharacterBasedEvents:
END_MAIN

LocationBasedEvents:
Village(0x8,VillageEvent,1,7)
House(0x9,RecruitElena,11,12)
END_MAIN

MiscBasedEvents:
AFEV 0x0 CheckNoUnits 0x0
DefeatAll(EndingScene)
END_MAIN

Dunno:
END_MAIN

Tutorial:
END_MAIN

TrapData:
END_MAIN

ALIGN 4

Units:
UNIT

BeginningScene:
LOMA 0x39 //cutscene map
MUSC WindTrack //wind
WEATHER 6
FADU 2
CURSOR [7,8]
STAL 120
REMOVE_CURSORS
SetBackground(0x00)
Text(Ch1CutsceneText)
EndConvo
MUSCSLOW Silent
FADI 3
WEATHER 0 
LOMA 0x00 //playable map
LOAD1 1 EnemyUnits
ENUN
CAMERA [0,14]
FADU 5
STAL 30
CAMERA [19,0]
LOAD1 1 PlayerUnits
ENUN
SetBackground(0x30)
Text(Ch1StartText)
EndConvo
CAMERA [0,14]
STAL 30
MUSC Tension
SetBackground(0x30)
Text(Ch1BadGuyText)
EndConvo
MOVE 5 Amani6 [0,12]
ENUN
CAMERA [19,0]
STAL 30
SetBackground(0x30)
Text(Ch1StartText2)
EndConvo
CAMERA [19,9] // friends come in
LOAD1 1 Buddies
ENUN
SetBackground(0x30)
Text(Ch1BuddiesText)
EndConvo
NoFade
CAMERA [19,14]
CURSOR [11,12] //highlight house
STAL 60
REMOVE_CURSORS
SetBackground(0x01)
Text(Ch1ElenaOpening)
EndConvo
NoFade
CAMERA [19,0]
ENDA

EndingScene:
MUSC 0x92
SetBackground(0x30)
Text(Ch1EndingText)
//EndConvo
//FADI 3
MNC2 0x1 //MNC2 works with prologue chapter
ENDA

CheckNoUnits:
ASMC GetBlueUnitCountASMC
SVAL 0x7 0x4
BLT 0x0 0xC 0x7 // If there are less than 2 alive, game over.
	GOTO 0x01
LABEL 0x00
ASMC 0x00D391 //GameOver
LABEL 0x01
NoFade
ENDA

///

VillageEvent:
VillageEventItem(Ch1VillageText,0x3,GoddessIcon)
ENDA

RecruitElena:
MUSI
MUSS 0x30 //comrades
SetBackground(0x0)
Text(Ch1TalkElena)
EndConvo
MURE 2
LOAD1 1 ElenaSpawn
ENUN
NoFade
ENDA

FriendsCome:
CAMERA [19,14]
LOAD1 1 Buddies
ENUN
Text(Ch1BuddiesText)
EndConvo
NoFade
ENDA

EnemiesCome1:
CAMERA [0,14]
LOAD1 0 EnemyReinforcements1
STAL 10
ENUN
NoFade
ENDA

///

PlayerUnits:

UNIT Naiah AmaniFemaleClass Naiah Level(3,Ally,False) [19,4] 0x0 0x0 0x1 REDA15R4 [FireFang,Vulnerary] NoAI
UNIT Idi AmaniMaleClass Naiah Level(5,Ally,False) [19,3] 0x0 0x0 0x1 REDA16R3 [FireFang,Vulnerary] NoAI
UNIT

EnemyUnits:

UNIT Bulren Brigand Bulren Level(5,Enemy,False) [1,13] 0x0 0x0 0x0 0x1 [HandAxe] NeverMoveAI
UNIT Amani6 AmaniMaleClass Bulren Level(3,Enemy,True) [0,13] 0x0 0x0 0x0 0x1 [FireFang] NeverMoveAI
UNIT Amani12 AmaniMaleClass Bulren Level(3,Enemy,True) [5,5] 0x0 0x0 0x0 0x1 [FireFang] NeverMoveAI
UNIT DarkMage Shaman Bulren Level(6,Enemy,True) [2,14] 0x0 0x0 0x0 0x1 [Flux] AttackInRangeAI
UNIT AxeBandit Brigand Bulren Level(3,Enemy,True) [9,10] 0x0 0x0 0x0 0x1 [IronAxe] BrigandAI
UNIT AxeBandit Brigand Bulren Level(3,Enemy,True) [13,6] 0x0 0x0 0x0 0x1 [IronAxe] AttackInRangeAI
UNIT AxeBandit Brigand Bulren Level(4,Enemy,True) [8,7] 0x0 0x0 0x0 0x1 [IronAxe] DefaultAI
UNIT AxeBandit Brigand Bulren Level(3,Enemy,True) [10,1] 0x0 0x0 0x0 0x1 [IronAxe] DefaultAI
//UNIT BowBandit Archer Bulren Level(5,Enemy,True) [5,10] 0x0 0x0 0x0 0x1 [IronBow] AttackInRangeAI
//UNIT BowBandit Archer Bulren Level(5,Enemy,True) [15,14] 0x0 0x0 0x0 0x1 [IronBow] AttackInRangeAI
UNIT AnimaMage Mage Bulren Level(6,Enemy,True) [7,14] 0x0 0x0 0x0 0x1 [Fire] AttackInRangeAI
UNIT

Buddies:
UNIT Safiyah Archer_F Naiah Level(3,Ally,False) [19,9] 0x0 0x0 0x1 REDA16R9 [IronBow,Vulnerary] NoAI
UNIT Ayanna Monk_F Naiah Level(2,Ally,False) [19,8] 0x0 0x0 0x1 REDA17R9 [Lightning,Vulnerary] NoAI
UNIT

ElenaSpawn:
UNIT Elena Cleric Naiah Level(3,Ally,False) [11,12] 0x0 0x0 0x1 REDA11R13 [Heal, Vulnerary] NoAI
UNIT

EnemyReinforcements1:
UNIT AxeBandit Fighter Bulren Level(4,Enemy,True) [0,13] 0x0 0x0 0x0 0x1 [IronAxe] DefaultAI
UNIT AxeBandit Fighter Bulren Level(4,Enemy,True) [0,14] 0x0 0x0 0x1 REDA1R14 [IronAxe] DefaultAI
UNIT

///

MESSAGE Chapter1 ends at offset CURRENTOFFSET